plugins {
    id "com.android.application"
    id "kotlin-android"
    // Le plugin Flutter doit être appliqué après les plugins Android et Kotlin
    id "dev.flutter.flutter-gradle-plugin"
}

android {
    namespace = "com.example.flutterdoc"
    compileSdk = flutter.compileSdkVersion
    ndkVersion = flutter.ndkVersion

    compileOptions {
        // Cibler Java 17 pour supprimer les warnings sur la version 8 obsolète
        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17
    }

    kotlinOptions {
        // Générer du bytecode Java 17 pour Kotlin
        jvmTarget = "17"
    }

    defaultConfig {
        applicationId = "com.example.flutterdoc"
        minSdk = flutter.minSdkVersion
        targetSdk = flutter.targetSdkVersion
        versionCode = flutter.versionCode
        versionName = flutter.versionName
    }
    // Configuration des flavors pour les environnements de production et développement
    flavorDimensions "default"
    productFlavors {
        production {
            dimension = "default"
            // android:label="@string/app_name" cette valeur est définie dans AndroidManifest.xml
            //pour chaque flavor via resValue ci-dessous suivant l'environnement 
            //production ou development
            resValue  "string", "app_name", "DocDoc Production"
        }
        development {
            dimension = "default"
            // Suffixe pour l'ID de l'application en développement
            applicationIdSuffix = ".dev"
            resValue "string", "app_name", "DocDoc Development"
        }
    }

    buildTypes {
        release {
            // Pour l'instant, on signe en debug pour pouvoir faire flutter run --release
            signingConfig = signingConfigs.debug
        }
    }
}

flutter {
    source = "../.."
}
